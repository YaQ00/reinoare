*start
if %1009!=1 gosub *configset1st ;初プレイの場合の各数値初期設定
bgmfadeout 2000 ;BGMのフェード 任意の数字を、不要の場合は削除を
bgmfadein 2000

saveoff

if %1010=1 goto *logojump
bg "image\logotop.png",10,1500  ;ロゴ表示 リセット時には飛ばす
delay 300
;dwave 1,"sound\guitar_jingle.wav"  ;ジングル
;delay 3800 ;ジングルの長さに合わせて変更
*logojump
bg black,10,500 ;一旦画面を黒に
delay 500
mov %1010,0
bg "image\Title.jpg",10,1000
bgm "sound\October 14th.ogg"

*title
;ver.2.01から修正。
;ロード画面やコンフィグ画面とスプライト番号が重ならないようにしました。

lsp 40,":a;image\start1.png",760,300
lsph 41,":a;image\start2.png",760,300
lsp 42,":a;image\load1.png",760,360
lsph 43,":a;image\load2.png",760,360
lsp 44,":a;image\config1.png",760,420
lsph 45,":a;image\config2.png",760,420
lsp 46,":a;image\exit1.png",760,480
lsph 47,":a;image\exit2.png",760,480

print 1

*title_loop
;ver.2.01から修正。bspで効果音再生。

bclear

bsp 40,"P40C41","P41C40","S10,(sound\finger_snap.wav)"
bsp 42,"P42C43","P43C42","S10,(sound\finger_snap.wav)"
bsp 44,"P44C45","P45C44","S10,(sound\finger_snap.wav)"
bsp 46,"P46C47","P47C46",""

bexec $0

if $0=="S40" stop:goto *story_start
if $0=="S42" gosub *loadset:goto *title
if $0=="S44" gosub *config:goto *title
if $0=="S46" gosub *enddal:goto *title

goto *title_loop
